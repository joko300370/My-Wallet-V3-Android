apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'com.novoda.build-properties'
apply from: '../quality/jacocoApp.gradle'
apply from: '../quality/ktlint.gradle'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlinx-serialization'


buildProperties {
    secrets {
        using(file('secrets.properties')) | file('secretsDefaults.properties')
    }
}

def getGitCommitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    signingConfigs {
        debug {
            storeFile file('blockchainDebugKey.keystore')
            storePassword buildProperties.secrets['keyPassword'].string
            keyPassword buildProperties.secrets['keyPassword'].string
            keyAlias buildProperties.secrets['debugKeyAlias'].string
        }

        prodInternal {
            storeFile file('blockchainInternalKey.keystore')
            storePassword buildProperties.secrets['keyPasswordInternal'].string
            keyPassword buildProperties.secrets['keyPasswordInternal'].string
            keyAlias buildProperties.secrets['internalKeyAlias'].string
        }
    }
    compileSdkVersion Versions.compileSdk
    buildToolsVersion Versions.buildTools
    dexOptions.javaMaxHeapSize "3g"

    defaultConfig {
        applicationId "piuk.blockchain.android"
        targetSdkVersion Versions.targetSdk
        minSdkVersion Versions.minSdk
        versionCode Versions.versionCode
        versionName Versions.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        archivesBaseName = "blockchain-$versionName"

        // Flag to hide or show the Contacts feature
        buildConfigBoolean "LOG_KOIN_STARTUP", false
        buildConfigBoolean "SKIP_VERIFF_KYC", false

        buildConfigString "SHAPE_SHIFT_API_KEY", buildProperties.secrets['shapeShiftApiKey']

        manifestPlaceholders = [
                FABRIC_API_KEY: buildProperties.secrets['fabricApiKey'].string
        ]

        resConfigs 'en',
                'de',
                'es',
                'fr',
                'it',
                'pt',
                'pt-rBR',
                'ru',
                'tr'
    }

    bundle {
        language {
            // Specifies that the app bundle should not support
            // configuration APKs for language resources. These
            // resources are instead packaged with each base and
            // dynamic feature APK.
            enableSplit = false
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }

    flavorDimensions "environment"

    productFlavors {
        /**
         * Environments
         */
        envProd {
            dimension "environment"
            manifestPlaceholders = [
                    BACKUP_API_KEY     : buildProperties.secrets['prodGoogleBackupApiKey'].string,
                    DYNAMIC_LINK_HOST  : buildProperties.secrets['prodDynamicLink'].string,
                    DYNAMIC_LINK_HOST_2: buildProperties.secrets['prodDynamicLink2'].string,
                    LOGIN_LINK_HOST    : buildProperties.secrets['prodLoginLink'].string,
                    LOGIN_LINK_HOST_2  : buildProperties.secrets['prodLoginLink2'].string
            ]
            buildConfigString "EXPLORER_URL", buildProperties.secrets['prodExplorer']
            buildConfigString "API_URL", buildProperties.secrets['prodApiServer']
            buildConfigString "EVERYPAY_HOST_URL", buildProperties.secrets['prodEverypayHost']
            buildConfigString "ETHEREUM_WEBSOCKET_URL", buildProperties.secrets['prodEthWebsocket']
            buildConfigString "BITCOIN_CASH_WEBSOCKET_URL", buildProperties.secrets['prodBchWebsocket']
            buildConfigString "STATUS_API_URL", buildProperties.secrets['prodApiStatus']
            buildConfigString "NABU_WEBSOCKET_URL", buildProperties.secrets['prodNabuWebsocket']
            buildConfigString "HORIZON_URL", buildProperties.secrets['prodHorizonUrl']
            buildConfigString "PIT_LINKING_URL", buildProperties.secrets['prodPitDoLink']
            buildConfigString "PIT_LAUNCHING_URL", buildProperties.secrets['prodPitLaunch']
            buildConfigString "COINS_WEBSOCKET_URL", buildProperties.secrets['prodCoinsWebsocket']
            buildConfigField "String", "ENVIRONMENT", '"env_prod"'
            buildConfigField "String", "VERSION_NAME_SUFFIX", "\"" + versionNameSuffix + "\""
            buildConfigField "String", "COMMIT_HASH", "\"\""
            buildConfigString "SIFT_ACCOUNT_ID", buildProperties.secrets['prodSiftAccount']
            buildConfigString "SIFT_BEACON_KEY", buildProperties.secrets['prodSiftBeaconId']
            buildConfigString "RECAPTCHA_SITE_KEY", buildProperties.secrets['captchaProdSiteKey']
        }
        envInternalProd {
            dimension "environment"
            manifestPlaceholders = [
                    BACKUP_API_KEY     : buildProperties.secrets['prodGoogleBackupApiKey'].string,
                    DYNAMIC_LINK_HOST  : buildProperties.secrets['prodDynamicLink'].string,
                    DYNAMIC_LINK_HOST_2: buildProperties.secrets['prodDynamicLink2'].string,
                    LOGIN_LINK_HOST    : buildProperties.secrets['prodLoginLink'].string,
                    LOGIN_LINK_HOST_2  : buildProperties.secrets['prodLoginLink2'].string
            ]
            buildConfigString "EXPLORER_URL", buildProperties.secrets['prodExplorer']
            buildConfigString "API_URL", buildProperties.secrets['prodApiServer']
            buildConfigString "EVERYPAY_HOST_URL", buildProperties.secrets['prodEverypayHost']
            buildConfigString "ETHEREUM_WEBSOCKET_URL", buildProperties.secrets['prodEthWebsocket']
            buildConfigString "BITCOIN_CASH_WEBSOCKET_URL", buildProperties.secrets['prodBchWebsocket']
            buildConfigString "STATUS_API_URL", buildProperties.secrets['prodApiStatus']
            buildConfigString "NABU_WEBSOCKET_URL", buildProperties.secrets['prodNabuWebsocket']
            buildConfigString "HORIZON_URL", buildProperties.secrets['prodHorizonUrl']
            buildConfigString "PIT_LINKING_URL", buildProperties.secrets['prodPitDoLink']
            buildConfigString "PIT_LAUNCHING_URL", buildProperties.secrets['prodPitLaunch']
            buildConfigString "COINS_WEBSOCKET_URL", buildProperties.secrets['prodCoinsWebsocket']
            buildConfigField "String", "ENVIRONMENT", '"env_prod"'
            buildConfigField "String", "VERSION_NAME_SUFFIX", "\"" + versionNameSuffix + "\""
            buildConfigField "String", "COMMIT_HASH", "\"\""
            buildConfigString "SIFT_ACCOUNT_ID", buildProperties.secrets['prodSiftAccount']
            buildConfigString "SIFT_BEACON_KEY", buildProperties.secrets['prodSiftBeaconId']
            buildConfigString "RECAPTCHA_SITE_KEY", buildProperties.secrets['captchaProdSiteKey']

            applicationId "com.blockchain.wallet.internal"
            resValue "string", "app_name", "BlockInternal"

            def ciVersionCode = System.getenv("BITRISE_BUILD_NUMBER")
            if (ciVersionCode != null) {
                versionCode = ciVersionCode.toInteger()
                versionName = ciVersionCode
                versionNameSuffix = ".$ciVersionCode"
            } else {
                versionCode = 1
                versionName = 1
                versionNameSuffix = ".local"
            }
        }
        envDev {
            dimension "environment"
            manifestPlaceholders = [
                    BACKUP_API_KEY     : buildProperties.secrets['devGoogleBackupApiKey'].string,
                    DYNAMIC_LINK_HOST  : buildProperties.secrets['devDynamicLink'].string,
                    DYNAMIC_LINK_HOST_2: buildProperties.secrets['devDynamicLink2'].string,
                    LOGIN_LINK_HOST    : buildProperties.secrets['devLoginLink'].string,
                    LOGIN_LINK_HOST_2  : buildProperties.secrets['devLoginLink2'].string
            ]
            buildConfigString "EXPLORER_URL", buildProperties.secrets['devExplorer']
            buildConfigString "API_URL", buildProperties.secrets['devApiServer']
            buildConfigString "EVERYPAY_HOST_URL", buildProperties.secrets['stagingEverypayHost']
            buildConfigString "ETHEREUM_WEBSOCKET_URL", buildProperties.secrets['devEthWebsocket']
            buildConfigString "BITCOIN_CASH_WEBSOCKET_URL", buildProperties.secrets['devBchWebsocket']
            buildConfigString "NABU_WEBSOCKET_URL", buildProperties.secrets['devNabuWebsocket']
            buildConfigString "HORIZON_URL", buildProperties.secrets['devHorizonUrl']
            buildConfigString "PIT_LINKING_URL", buildProperties.secrets['devPitDoLink']
            buildConfigString "PIT_LAUNCHING_URL", buildProperties.secrets['devPitLaunch']
            buildConfigString "STATUS_API_URL", buildProperties.secrets['devApiStatus']
            buildConfigString "COINS_WEBSOCKET_URL", buildProperties.secrets['devCoinsWebsocket']
            buildConfigField "String", "ENVIRONMENT", '"env_dev"'
            buildConfigField "String", "COMMIT_HASH", "\"${getGitCommitHash()}\""
            buildConfigField "String", "VERSION_NAME_SUFFIX", "\"" + versionNameSuffix + "\""
            buildConfigString "SIFT_ACCOUNT_ID", buildProperties.secrets['devSiftAccount']
            buildConfigString "SIFT_BEACON_KEY", buildProperties.secrets['devSiftBeaconId']
            buildConfigString "RECAPTCHA_SITE_KEY", buildProperties.secrets['captchaDevSiteKey']

            versionNameSuffix '-dev'
            applicationId "piuk.blockchain.android.dev"
            resValue "string", "app_name", "BlockDev"
        }
        envStaging {
            dimension "environment"
            manifestPlaceholders = [
                    BACKUP_API_KEY     : buildProperties.secrets['stagingGoogleBackupApiKey'].string,
                    DYNAMIC_LINK_HOST  : buildProperties.secrets['stagingDynamicLink'].string,
                    DYNAMIC_LINK_HOST_2: buildProperties.secrets['stagingDynamicLink2'].string,
                    LOGIN_LINK_HOST    : buildProperties.secrets['stagingLoginLink'].string,
                    LOGIN_LINK_HOST_2  : buildProperties.secrets['stagingLoginLink2'].string
            ]
            buildConfigString "EXPLORER_URL", buildProperties.secrets['stagingExplorer']
            buildConfigString "API_URL", buildProperties.secrets['stagingApiServer']
            buildConfigString "EVERYPAY_HOST_URL", buildProperties.secrets['stagingEverypayHost']
            buildConfigString "STATUS_API_URL", buildProperties.secrets['stagingApiStatus']
            buildConfigString "ETHEREUM_WEBSOCKET_URL", buildProperties.secrets['stagingEthWebsocket']
            buildConfigString "BITCOIN_CASH_WEBSOCKET_URL", buildProperties.secrets['stagingBchWebsocket']
            buildConfigString "NABU_WEBSOCKET_URL", buildProperties.secrets['stagingNabuWebsocket']
            buildConfigString "HORIZON_URL", buildProperties.secrets['stagingHorizonUrl']
            buildConfigString "PIT_LINKING_URL", buildProperties.secrets['stagingPitDoLink']
            buildConfigString "PIT_LAUNCHING_URL", buildProperties.secrets['stagingPitLaunch']
            buildConfigString "COINS_WEBSOCKET_URL", buildProperties.secrets['stagingCoinsWebsocket']
            buildConfigField "String", "ENVIRONMENT", '"env_staging"'
            buildConfigField "String", "COMMIT_HASH", "\"${getGitCommitHash()}\""
            buildConfigField "String", "VERSION_NAME_SUFFIX", "\"" + versionNameSuffix + "\""
            buildConfigString "SIFT_ACCOUNT_ID", buildProperties.secrets['stagingSiftAccount']
            buildConfigString "SIFT_BEACON_KEY", buildProperties.secrets['stagingSiftBeaconId']
            buildConfigString "RECAPTCHA_SITE_KEY", buildProperties.secrets['captchaStagingSiteKey']

            versionNameSuffix '-staging'
            applicationId "piuk.blockchain.android.staging"
            resValue "string", "app_name", "BlockStaging"
        }
    }

    buildTypes {
        release {
            productFlavors.envInternalProd.signingConfig signingConfigs.prodInternal
            debuggable false
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            multiDexEnabled true
        }
        debug {
            multiDexEnabled true
            buildConfigBoolean "SKIP_VERIFF_KYC", false
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds true
        fatal 'StopShip'
        htmlReport true
        htmlOutput file("build/reports/lint-report.html")
        abortOnError true
        baseline file("lint-baseline.xml")
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/rxjava.properties'
        exclude 'jni/**'
        exclude 'solidity/**'
        exclude 'org.bitcoin.test.checkpoints.txt'
        exclude 'org.bitcoin.production.checkpoints.txt'
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    testOptions {
        // Fix for Robolectric bug when using AAPT2
        unitTests.includeAndroidResources true
        // Always show the result of every unit test, even if it passes.
        unitTests.all {
            testLogging {
                if (System.getenv().containsKey("CIRCLE_BUILD_NUM")) {
                    events 'skipped', 'failed'
                } else {
                    events 'skipped', 'failed', 'standardOut', 'standardError'
                }
            }
            reports.junitXml.destination = file('../build/test-results/')
            maxHeapSize = "1g"
        }
    }
}

kapt {
    useBuildCache = true
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation project(':coreui')
    implementation project(':notifications')
    implementation project(':nabu')
    implementation project(':sunriver')
    implementation project(':core')

    implementation project(':coreui')
    implementation project(':notifications')
    implementation project(':veriff')
    implementation project(':blockchainApi')

    implementation Libraries.multidex
    coreLibraryDesugaring Libraries.desugaring

    implementation Libraries.koin
    implementation Libraries.koinAndroid

    // Views
    implementation Libraries.countryPicker
    implementation Libraries.bottomNav
    implementation Libraries.circleIndicator
    implementation Libraries.charts
    implementation Libraries.sparkline
    implementation Libraries.cardForm
    implementation Libraries.zxing
    implementation Libraries.sift

    // Google core
    implementation Libraries.googlePlayCore

    api Libraries.appCompat
    implementation Libraries.navigationControllerCore
    implementation Libraries.navigationControllerFragments

    implementation Libraries.lifecycleViewModel
    implementation Libraries.lifecycleExtensions

    implementation Libraries.camera
    implementation Libraries.cameraView
    implementation Libraries.cameraLifecycle

    implementation(Libraries.googlePlayServicesBase) {
        exclude group: 'com.android.support'
    }
    implementation Libraries.googlePlayServicesAuth
    implementation Libraries.googlePlaces
    implementation Libraries.googleServicesReCaptcha
    implementation Libraries.biometricsApi

    implementation Libraries.gson
    implementation Libraries.kotlinJson
    implementation Libraries.json

    // Utils
    implementation Libraries.libPhoneNumber
    implementation Libraries.yearclass
    implementation Libraries.stetho
    implementation Libraries.stethoOkHttp

    // Navigation
    implementation Libraries.navigationControllerCore
    implementation Libraries.navigationControllerFragments
    // RxJava
    implementation Libraries.rxReplayShare
    implementation Libraries.rxRelay

    // Glide
    implementation Libraries.glide
    annotationProcessor Libraries.glideAnnotations

    // Custom Views
    implementation Libraries.materialDatePicker

    // UI/Instrumentation Tests
    androidTestImplementation(Libraries.espresso) {
        exclude group: 'javax.inject', module: 'support-annotations'
    }
    androidTestImplementation Libraries.mockito
    androidTestImplementation(Libraries.mockitoKotlin) {
        ['kotlin-stdlib', 'kotlin-reflect'].each { exclude module: it }
    }

    // Unit Tests
    testImplementation Libraries.junit
    testImplementation Libraries.mockito
    testImplementation Libraries.hamcrestJunit
    testImplementation Libraries.kotlinJunit
    testImplementation Libraries.kluent
    testImplementation Libraries.jacksonKotlin
    testImplementation(Libraries.mockitoKotlin) {
        ['kotlin-stdlib', 'kotlin-reflect'].each { exclude module: it }
    }

    testImplementation(Libraries.robolectric) {
        exclude group: 'com.google.guava'
        exclude group: 'com.google.protobuf'
    }

    // For AS running of robolectric
    androidTestImplementation Libraries.testRunner
    androidTestImplementation Libraries.testRules

    testImplementation project(':testutils-android')

    debugImplementation Libraries.chuckerDebug
    releaseImplementation Libraries.chuckerRelease
}
